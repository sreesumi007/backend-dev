buildscript {
    dependencies {
        classpath('org.openapitools:openapi-generator:6.2.1')
    }
}

plugins {
    id 'org.springframework.boot' version '2.7.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

group = 'de.tudresden.inf.st'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    developmentOnly 'com.h2database:h2'
    testImplementation 'com.h2database:h2'
    //implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.rabbitmq:amqp-client:5.16.0'
    //implementation 'org.springdoc:springdoc-openapi-ui:1.6.14'
    implementation 'io.swagger.core.v3:swagger-annotations:2.1.10'
    implementation 'javax.validation:validation-api:2.0.1.Final'
    implementation 'org.apache.logging.log4j:log4j-api:2.19.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.19.0'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'com.github.docker-java:docker-java:3.3.0'
    implementation 'com.github.docker-java:docker-java-transport-httpclient5:3.3.0'
    implementation 'org.postgresql:postgresql:42.5.1'
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java'
            srcDirs 'src/generated/java'
        }
    }
}

import org.openapitools.codegen.DefaultGenerator
import org.openapitools.codegen.config.CodegenConfigurator

def apiSpecSourceFile = 'openapi-spec/mathgrass-api.yaml'
def generatedApiTargetFolder = 'src/generated/java'
def generatedPlantUmlTargetFolder = 'openapi-spec'


tasks.register('generateApi') {
    doLast {
        project.delete(
                files('src/generated')
        )
        def springCodeGenConfig = new CodegenConfigurator()
        springCodeGenConfig.setInputSpec("file:///$projectDir/$apiSpecSourceFile")
        springCodeGenConfig.setOutputDir("$projectDir")
        springCodeGenConfig.setGeneratorName('spring')
        springCodeGenConfig.setAdditionalProperties([
                'interfaceOnly'        : 'true',
                'apiPackage'           : 'de.tudresden.inf.st.mathgrass.api.apiModel',
                'modelPackage'         : 'de.tudresden.inf.st.mathgrass.api.model',
                'sourceFolder'         : generatedApiTargetFolder
        ])
        new DefaultGenerator().opts(springCodeGenConfig.toClientOptInput()).generate()
        // unfortunately, a pom.xml is generated by the API generation - needs to be deleted!
        project.delete(
                files("pom.xml")
        )

        def plantUmlCodeGenConfig = new CodegenConfigurator()
        plantUmlCodeGenConfig.setInputSpec("file:///$projectDir/$apiSpecSourceFile")
        plantUmlCodeGenConfig.setOutputDir("$projectDir/$generatedPlantUmlTargetFolder")
        plantUmlCodeGenConfig.setGeneratorName('plantuml')
        new DefaultGenerator().opts(plantUmlCodeGenConfig.toClientOptInput()).generate()

    }
}

// execute generateApi task before building the whole project
compileJava {
    dependsOn generateApi
}
